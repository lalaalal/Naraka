import com.hypherionmc.modpublisher.properties.CurseEnvironment

plugins {
    id 'dev.architectury.loom' version '1.9-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.1.7"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name-$rootProject.supporting_minecraft_version"
    }

    loom {
        accessWidenerPath = project(":common").file("src/main/resources/naraka.common.accesswidener")
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchmentmc_mc_version}:${parchmentmc_version}@zip")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    ext.setupPublisher = {
        publisher {
            apiKeys {
                modrinth System.getenv("MODRINTH_TOKEN")
                curseforge System.getenv("CURSE_TOKEN")
            }

            setModrinthID("2InS5Swi")
            setCurseID("1292603")
            setVersionType("$rootProject.mod_version_type")
            setChangelog(rootProject.projectDir.toPath().resolve("change.md").toFile())
            setVersion("$rootProject.mod_version")
            setProjectVersion("$rootProject.mod_version")
            setJavaVersions(List.of(JavaVersion.VERSION_21))
            setGameVersions("1.21", "1.21.1")
            setDisplayName("${base.archivesName.get()}-${rootProject.mod_version}")
            setCurseEnvironment(CurseEnvironment.BOTH)
            setArtifact("build/libs/${base.archivesName.get()}-${rootProject.mod_version}.jar")
        }
    }
}
